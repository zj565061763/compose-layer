plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def libGroupId = "com.sd.lib.android"
def libArtifactId = "compose-layer"
def libVersionName = "1.0.0-alpha03"

android {
    namespace 'com.sd.lib.compose.layer'
    compileSdk 33
    defaultConfig {
        minSdk 21
        targetSdk 33
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        kotlinOptions.freeCompilerArgs += ['-module-name', "$libGroupId.$libArtifactId"]
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
}

dependencies {
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'com.github.zj565061763:aligner:1.0.0-alpha05'
}


task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = libGroupId
                artifactId = libArtifactId
                version = libVersionName

                // 上传源码
                artifact generateSourcesJar
            }
        }
    }
}